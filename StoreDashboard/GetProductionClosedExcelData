USE [Sap_Approval]
GO

SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

ALTER PROCEDURE [dbo].[GetProductionClosedExcelData]
    @Sloc_Code VARCHAR(50) = NULL  -- Optional filter
AS
BEGIN
    SET NOCOUNT ON;
 ---------------------------------------------
    -- 1. Summary View: Grouped by Supervisor
    ---------------------------------------------
    SELECT 
        SC.Supv_ID,
        CONCAT(SC.Sup_Code, ' - ', SC.Sup_Name) AS Sup_Name,
        COUNT(TP.Prdord_ID) AS [No_Of_Orders],
        COUNT(CASE WHEN TP.Order_Type = 'Closed' THEN 1 END) AS [No_Order_Close],
        COUNT(CASE WHEN TP.Order_Type = 'New' THEN 1 END) AS [No_Of_Open_Orders],
        COUNT(CASE WHEN TP.Ontime_or_Delay = 'g' THEN 1 END) AS [Issue_Posted_on_Time],
        COUNT(CASE WHEN TP.Ontime_or_Delay = 'y' THEN 1 END) AS [Issue_Posted_Delay60],
        COUNT(CASE WHEN TP.Ontime_or_Delay = 'r' THEN 1 END) AS [Issue_Posted_Delay]
    FROM Trn_prod_order_status TP
    LEFT JOIN Mst_SupvCode SC ON TP.Supv_ID = SC.Supv_ID
    LEFT JOIN (
        SELECT Order_No, Sloc_Code, Created_On
        FROM (
            SELECT *,
                   ROW_NUMBER() OVER (PARTITION BY Order_No ORDER BY Created_On DESC) AS rn
            FROM TrnSap_Order_creation
        ) OC
        WHERE rn = 1
    ) OC ON TP.Order_No = OC.Order_No
    WHERE
        (
            (TP.Order_Type = 'Closed' AND CAST(TP.Order_Closed_Date AS DATE) = CAST(GETDATE() AS DATE))
            OR
            (TP.Order_Type = 'New' AND CAST(OC.Created_On AS DATE) = CAST(GETDATE() AS DATE))
        )
        AND (@Sloc_Code IS NULL OR OC.Sloc_Code = @Sloc_Code)
    GROUP BY SC.Supv_ID, SC.Sup_Code, SC.Sup_Name;

    ---------------------------------------------
    -- 2. Detailed Split View
    ---------------------------------------------
    SELECT   
        TP.Prdord_ID,  
        TP.Plant_ID,  
        MP.Plant_Code AS TP_Plant_Code,  
        TP.Order_Type,  
        TP.Supv_ID,  
        CONCAT(MS.Sup_Code, ' - ', MS.Sup_Name) AS Sup_Name,  
        MSP.Plant_Code AS Supv_Plant_Code,  
        TP.Order_No,  
        FORMAT(TP.Order_Created_Date, 'dd:MM:yyyy HH:mm') AS Order_Date,
        TP.Issue_Duration_Time,  
        TP.Material_No,  
        TP.Material_Description,  
        TP.Order_Qty,  
        TP.Order_Closed_Date,
        TP.Ontime_or_Delay,

        RIGHT('0' + CAST(MS.Supv_Lead_Time / 60 AS VARCHAR), 2) + ':' +
        RIGHT('0' + CAST(MS.Supv_Lead_Time % 60 AS VARCHAR), 2) AS Supv_Lead_Time,

        RIGHT('0' + CAST(CA.DelayInMinutes / 60 AS VARCHAR), 2) + ':' +  
        RIGHT('0' + CAST(CA.DelayInMinutes % 60 AS VARCHAR), 2) AS Delay_Time,  

        CA.DelayInMinutes,  
        CASE   
            WHEN CA.DelayInMinutes <= CA.LeadTimeMinutes THEN 'Green'  
            WHEN CA.DelayInMinutes <= CA.LeadTimeMinutes + 60 THEN 'Yellow'  
            ELSE 'Red'  
        END AS Delay_Color,  

        ST.Storage_Code AS Creation_Storage_Code,  
        ST.SLoc_Name AS Creation_SLoc_Name  
    FROM Trn_prod_order_status TP  
    INNER JOIN Mst_Plant MP ON TP.Plant_ID = MP.Plant_ID  
    LEFT JOIN Mst_SupvCode MS ON TP.Supv_ID = MS.Supv_ID  
    LEFT JOIN Mst_Plant MSP ON MS.Plant_ID = MSP.Plant_ID  
    INNER JOIN (  
        SELECT *  
        FROM (  
            SELECT *, ROW_NUMBER() OVER (PARTITION BY Order_No ORDER BY Created_On DESC) AS rn  
            FROM TrnSap_Order_creation  
        ) x  
        WHERE rn = 1  
    ) OC ON OC.Order_No = TP.Order_No  
    LEFT JOIN Mst_Storage_Location ST ON OC.Sloc_Code = ST.Storage_Code AND ST.Active_Status = 1  
    CROSS APPLY (  
        SELECT   
            DelayInMinutes = DATEDIFF(MINUTE, OC.Order_Date, GETDATE()),  
            LeadTimeMinutes = COALESCE(MS.Supv_Lead_Time, 0)  
    ) CA  
    WHERE 
        (
            (TP.Order_Type = 'Closed' AND CAST(TP.Order_Closed_Date AS DATE) = CAST(GETDATE() AS DATE))
            OR
            (TP.Order_Type = 'New' AND CAST(OC.Created_On AS DATE) = CAST(GETDATE() AS DATE))
        )
        AND (@Sloc_Code IS NULL OR OC.Sloc_Code = @Sloc_Code);
END;


EXEC [dbo].[GetProductionClosedExcelData] @Sloc_Code = '1150';
